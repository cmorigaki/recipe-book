// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
        google()
        gradlePluginPortal() // ben-manes
    }
    ext {
        kotlinVersion = "1.5.20"
        gradleVersion = '7.0.2'
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.39.0"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// Code coverage to sonar
plugins {
    id "org.sonarqube" version "3.1.1"
}
apply from: "$rootDir/quality/sonar-cloud.gradle"

// Dependency version definitions
apply from: "project-config/dependencies.gradle"

// Dependency version checker
// Usage: ./gradlew dependencyUpdates -Drevision=release
apply plugin: "com.github.ben-manes.versions"
apply from: "project-config/dependencies-checker/dependencies-checker.gradle"

// Test coverage
// Usage ./gradlew jacocoTestReport
apply from: "$rootDir/quality/test-coverage-setup.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}


// Ktlint
subprojects {
    apply from: "$rootDir/quality/ktlint.gradle"
}

task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'git-hooks/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}
tasks.getByPath(':app:preBuild').dependsOn installGitHook

task clean(type: Delete) {
    delete rootProject.buildDir
}
