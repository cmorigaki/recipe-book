plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.easylauncher)
}

apply from: "$rootDir/project-config/api-properties/api-properties.gradle"

android {
    compileSdk libs.versions.compileSdk.get() as int
    namespace 'br.com.recipebook'

    defaultConfig {
        applicationId "br.com.recipebook"

        minSdk libs.versions.minSdk.get() as int
        targetSdk libs.versions.compileSdk.get() as int
        versionCode 10010001 // x.xxx.xx-xx
        versionName "1.1.0"
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            applicationIdSuffix ".debug"

            testCoverageEnabled = true
        }
    }
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = jvmTargetVersion
    }
}

dependencies {
    implementation libs.kotlin.stdlib
    implementation libs.coroutines.core

    implementation platform(libs.koin.bom)
    implementation libs.koin.core
    implementation libs.koin.android

    implementation project(ProjectConfig.coreAndroid)
    implementation project(ProjectConfig.utilityAndroid)
    implementation project(ProjectConfig.utilityKotlin)
    implementation project(ProjectConfig.startup)
    implementation project(ProjectConfig.configuration)
    implementation project(ProjectConfig.navigation)
    implementation project(ProjectConfig.analytics)
    implementation project(ProjectConfig.analyticsAmplitude)
    implementation project(ProjectConfig.image)
    implementation project(ProjectConfig.monitoring)
    implementation project(ProjectConfig.device)

    implementation libs.sentry
    implementation libs.timber

    implementation project(Features.recipeCollection)
    implementation project(Features.recipeDetail)
    implementation project(Features.settings)
    implementation project(Features.settingsTheme)
    implementation project(Features.inAppUpdate)

    testImplementation libs.junit
    testImplementation libs.mockk

    // debugImplementation because LeakCanary should only run in debug builds.
    debugImplementation libs.leak.canary
}
